import java.nio.file.Paths

buildscript {
    repositories {
        gradlePluginPortal()
    }
}

plugins {
    id 'java-library'
    id 'org.ajoberstar.grgit' version '5.2.2'
}


group 'com.testia.plugins'
version '1.0.0'

def ndtkitDir = "C:/Program Files/NDTkit" // Change this path with your actual NDTkit installation folder

repositories {
    mavenCentral()
}

static def getVersionFromDir(String ndtkitDir) {
    def versionFile = new File(ndtkitDir, "version.txt")
    if (versionFile.exists()) {
        def content = versionFile.text.trim()
        def matcher = content =~ /(\d+\.\d+\.)/
        if (matcher.find()) {
            return matcher.group(1)
        }
    }
    return null
}

dependencies {

    // pf4j plugin
    compileOnly group: 'org.pf4j', name: 'pf4j', version: '3.10.0'
    compileOnly group: 'args4j', name: 'args4j', version: '2.37'
    testImplementation group: 'args4j', name: 'args4j', version: '2.37'

    def commonsTree = fileTree(dir: "${ndtkitDir}/lib", include: '**/ndt-commons-*.jar', exclude: '*-test.jar')
    def flawmodelTree = fileTree(dir: "${ndtkitDir}/lib", include: '**/ndt-flawmodel-*.jar')
    def ndtkitJar = files("${ndtkitDir}/NDTkit.jar")

    // --- Validation (Check if empty) ---
    if (commonsTree.isEmpty()) {
        throw new GradleException("MISSING DEPENDENCY: ndt-commons-*.jar not found in ${ndtkitDir}/lib")
    }
    if (flawmodelTree.isEmpty()) {
        throw new GradleException("MISSING DEPENDENCY: ndt-flawmodel-*.jar not found in ${ndtkitDir}/lib")
    }
    if (ndtkitJar.isEmpty() || !ndtkitJar.singleFile.exists()) {
        throw new GradleException("MISSING DEPENDENCY: NDTkit.jar not found at ${ndtkitDir}/NDTkit.jar")
    }

    compileOnly commonsTree
    testImplementation fileTree(dir: "${ndtkitDir}/lib", include: 'ndt-commons-*-test.jar')

    compileOnly flawmodelTree

    compileOnly ndtkitJar
    testImplementation ndtkitJar

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

}

jar {
    def ndtkitVersion = getVersionFromDir(ndtkitDir)
    if (ndtkitVersion != null) {
        def userHome = System.getProperty("user.home")
        def pluginDir = Paths.get(userHome, ".ndtkit", "Conf_${ndtkitVersion}", "plugins").toFile()
        destinationDirectory.set(pluginDir)
    } else {
        destinationDirectory.set(file("$ndtkitDir/lib/plugins"))
    }
    manifest {
        attributes(
                // For PF4J
                "Plugin-Class": "com.testia.ndtkit.plugin.pythonplugin.pf4j.PythonPlugin",
                "Plugin-Id": "python-plugin",
                "Plugin-License": "TESTIA-licencing",
                "Plugin-Version": "1.0.0",
                "Plugin-Description": "NDTkit Python plugin",

                // Other metadata
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
        )
    }
    doLast {
        print("Please restart NDTkit to activate it")
    }
}

dependencies {
    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    // pf4j plugin
    compileOnly group: 'org.pf4j', name: 'pf4j', version: '3.10.0'
    compileOnly group: 'args4j', name: 'args4j', version: '2.37'
    testImplementation group: 'args4j', name: 'args4j', version: '2.37'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

test {
    useJUnitPlatform()
}

tasks.named('jar') {
    exclude '**/python-source-code/**'
}

tasks.register('installJar') {
    dependsOn jar
}
